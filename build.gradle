buildscript {
    ext.kotlin_version = '1.4.21'

    repositories {
        google()
        jcenter()
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:4.1.2'
        classpath 'com.github.dcendents:android-maven-gradle-plugin:2.1'
        classpath 'com.google.gms:google-services:4.3.4'
        classpath 'com.google.firebase:firebase-crashlytics-gradle:2.4.1'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jlleitschuh.gradle:ktlint-gradle:9.4.1"
    }
}

subprojects {
    apply plugin: "org.jlleitschuh.gradle.ktlint"

    // Optionally configure plugin
    ktlint {
        debug = false
    }
}

allprojects {
    repositories {
        google()
        jcenter()
        maven { url 'https://jitpack.io' }
        maven {
            url 'https://artifactory.mxtracks.info/artifactory/gradle-dev-local/'
        }
    }
}

@SuppressWarnings("unused")
static def getGitCommitCount() {
    def process = "git rev-list HEAD --count".execute()
    return process.text.toInteger()
}

@SuppressWarnings("unused")
static def getTag() {
    def tagVersion = "$System.env.TAG_VERSION"
    if (tagVersion == "null") {
        println "No variable TAG_VERSION specified, I'm using 'git describe --abbrev=0'"
        def process = "git describe --tag".execute()
        tagVersion = process.text.toString().trim()
    } else {
        def tagVersionToken = tagVersion.split("/")
        tagVersion = tagVersionToken[2]
    }
    return tagVersion
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
